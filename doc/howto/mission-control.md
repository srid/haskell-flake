---
slug: mission-control
---

# Devshell scripts using mission-control

Mission-control provides the user of devshell with a menu that can be configured to provide an array of commands. This makes it possible for the project's user to locate all of the commandsÂ they need (to get started) in one place.

## Add mission-control to inputs

```nix
{
  # Inside inputs
  mission-control.url = "github:Platonic-Systems/mission-control";
}
```

Import the flakeModule:
```nix
{
  # Inside mkFlake
  imports = [
    inputs.mission-control.flakeModule
  ];
}
```

## Add a script

### Docs

Following script will let you start a hoogle server by running `, docs` in your devshell:

```nix
{
  # Inside perSystem
  mission-control.scripts = {
    docs = {
      description = "Start Hoogle server for project dependencies";
      exec = ''
        echo http://127.0.0.1:8888
        hoogle serve -p 8888 --local
      '';
      category = "Dev Tools";
    };
  };
}
```
`exec` can be any shell script of type string or a value of type of package.
`category` defines the group that this script belongs to.

### Cabal repl

To start a cabal repl from your devShell on running  `, repl`, use:

```nix
{
  # Inside perSystem
  mission-control.scripts = {
    repl = {
      description = "Start the cabal repl";
      exec = ''
        cabal repl "$@"
      '';
      category = "Dev Tools";
    };
  };
}
```
`"$@"` is a placeholder for the arguments provided to `, repl`. If you wish to run an executable `foo` from your project in cabal repl, use this: `, repl exe:foo`. Similarly, for a library `bar` it will be `, run lib:bar`.

### treefmt

To run the formatter in your devshell with `, fmt`:
```nix
{ 
  # Inside perSystem
  mission-control.scripts = {
    fmt = {
      description = "Format the source tree";
      exec = "${lib.getExe config.treefmt.build.wrapper}";
      category = "Dev Tools";
    };
  };
}
```

Follow the instructions on [treefmt](https://haskell.flake.page/treefmt) to configure the formatter for your repo.

### Executable of type package

```nix
{
  # Inside perSystem
  # Add `lib` to the input params of perSystem
  mission-control.scripts = {
    bar = {
      description = "Run foo";
      exec = "${lib.getExe self'.packages.foo}";
      category = "Dev Tools";
    };
  };
}
```
Since `exec` accepts a value of type package, the above can be rewritten as `exec = self'.packages.foo`. The caveat is that the name of executable generated by `foo` is listed as a list of commands in the devshell instead of the key `bar`. There is an open issue for this and can be tracked here: https://github.com/Platonic-Systems/mission-control/issues/15

## Tips

### wrapperName

If you don't wish to run your command using `, <command>` you can change the `,` to be any string of your choice by setting the value of attribute `wrapperName`, as follows:
```nix
{
  # Inside perSystem
  mission-control = {
    wrapperName = "s";
  };
}
```

## Upcoming

- [Zsh and bash shell completion](https://github.com/Platonic-Systems/mission-control/issues/4)

## Example

- https://github.com/srid/haskell-template/blob/master/flake.nix
